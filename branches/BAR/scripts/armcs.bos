#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SIG_BUILD		4	

piece  base,doorlf, doorlb, doorrf, doorrb, nano, turret, ground;

static-var  statechg_DesiredState, statechg_StateChanging,
			notBuilding, oldHead;

// Signal definitions
#define SIG_MOVE			2


activatescr()
{
	move doorlf to z-axis [7] speed [10];
	move doorrf to z-axis [7] speed [10];
	move doorrb to z-axis [-7] speed [10];
	move doorlb to z-axis [-7] speed [10];
	sleep 700;


}

deactivatescr()
{
	set INBUILDSTANCE to 0;

	turn turret to y-axis <0> speed <270>;
	sleep 300;
	move doorlf to z-axis [0] speed [10];
	move doorrf to z-axis [0] speed [10];
	move doorrb to z-axis [0] speed [10];
	move doorlb to z-axis [0] speed [10];
	sleep 700;
}

SetDirection(heading)
{
	var spd;
	spd = (get ABS(oldHead) - get ABS(heading)) / 2;
	turn ground to y-axis heading speed spd;
	turn base to y-axis 0-heading speed spd;
	oldHead = heading;
}

RockOnWaves()
{
	var rockDir, rockAng, rockSpeed;
	rockDir = <-0.1>;
	rockAng = <-0.1>;
	while( TRUE )
	{
		while( get ABS(rockAng) < <1.31> )
		{
			rockSpeed = <1.45> - get ABS(rockAng);
			turn ground to x-axis rockAng speed rockSpeed*4;
			wait-for-turn ground around x-axis;
			rockAng = rockAng + rockDir;
		}
		rockDir = 0 - rockDir;
		rockAng = rockAng + rockDir + rockDir;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script RockOnWaves();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

waveSplash()
{
	while( TRUE )
	{
		turn base to x-axis <-3.0> speed <3.0>;
		move base to y-axis [0.23] speed [0.23];
		wait-for-turn base around x-axis;
		turn base to x-axis <0.0> speed <3.0>;
		move base to y-axis [0.0] speed [0.23];
		wait-for-turn base around x-axis;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	var waketime;
	start-script waveSplash();
	while( TRUE )
	{
		emit-sfx 2 from base;
		emit-sfx 2 from base;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if( waketime < 50 ) waketime = 50;
		sleep 15000 / waketime;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	turn base to x-axis <0.0> speed <3.0>;
	move base to y-axis [0.0] speed [0.23];
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState )
		{
			call-script deactivatescr();
			notBuilding = 1;
			currentstate = 1;
		}
		else
		{
			call-script activatescr();
			notBuilding = 0;
			currentstate = 0;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	notBuilding = 1;
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
	start-script SmokeUnit();
}

Activate()
{
	start-script RequestState(0);
}

Deactivate()
{
	start-script RequestState(1);
}

StartBuilding(heading, pitch)
{
	start-script RequestState(0);
	while( notBuilding ) sleep 100;
	signal SIG_BUILD;
	turn turret to y-axis heading speed <160.000000>;
	wait-for-turn turret around y-axis;
	set INBUILDSTANCE to 1;
}

StopBuilding()
{
	signal SIG_BUILD;	
	set-signal-mask SIG_BUILD;
	sleep 6000;
	set-signal-mask 0;
	signal SIG_BUILD;	
	start-script RequestState(1);
}

QueryNanoPiece(piecenum)
{
	piecenum = nano;
}

/*
TargetHeading(Func_Var_1)
{
	Static_Var_1 = 0 - Func_Var_1;
}
*/

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode ground type BITMAPONLY;
		explode base type BITMAPONLY;
		explode doorlb type FIRE | SMOKE | FALL;
		explode doorrf type BITMAPONLY;
		explode turret type BITMAPONLY;
		explode nano type FIRE | SMOKE | FALL;
		explode doorrb type BITMAPONLY;
		explode doorlf type BITMAPONLY;
		return(0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode ground type FALL;
		explode base type BITMAPONLY;
		explode doorlb type FIRE | SMOKE | FALL;
		explode doorrf type FIRE | SMOKE | FALL;
		explode turret type FIRE | SMOKE | FALL;
		explode nano type FIRE | SMOKE | FALL;
		explode doorrb type FIRE | SMOKE | FALL;
		explode doorlf type FIRE | SMOKE | FALL;
		return(0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode ground type FIRE | SMOKE | FALL;
		explode base type BITMAPONLY;
		explode doorlb type SMOKE | FALL;
		explode doorrf type EXPLODE_ON_HIT | SMOKE | FALL;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode nano type FIRE | SMOKE | FALL;
		explode doorrb type FIRE | SMOKE | FALL;
		explode doorlf type SMOKE | FALL;
		return(0);
	}
	corpsetype = 3 ;
		explode ground type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode base type BITMAPONLY;
		explode doorlb type EXPLODE_ON_HIT | FIRE | FALL;
		explode doorrf type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL;
		explode nano type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode doorrb type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode doorlf type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
}
