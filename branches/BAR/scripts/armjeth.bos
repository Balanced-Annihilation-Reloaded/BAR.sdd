#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  torso, rflare, lflare, pelvis, lleg, rleg, rthigh, lthigh,
       lturret, lbarrel, rbarrel, rturret, lexhaust, rexhaust, lfoot, rfoot;

static-var  bMoving, Static_Var_2, Static_Var_3, gun_3, restore_delay,
            statechg_DesiredState, statechg_StateChanging,
            moveSpeed, currentSpeed;

// Signal definitions
#define SIG_AIM_3			2


walk()
{
	while( bMoving )
	{
		//maxvelocity = 1.85-> 56 elmos/sec
		//animspeed 24 elmos/cycle
		//step 0
		if (bMoving)
		{
	
		turn rthigh to x-axis <0> speed <300.000000>;
		turn rleg to x-axis <0> speed <187.500000>;
		turn rfoot to x-axis <0> speed <75.000000>;

		turn lthigh to x-axis <-15> speed <525.000000>;
		turn lleg to x-axis <35> speed <412.500000>;
		turn lfoot to x-axis <-21> speed <45.000000>;

		move pelvis to y-axis [0] speed [3.750000];
		turn pelvis to z-axis <0> speed <22.500000>;
		turn pelvis to x-axis <0> speed <22.500000>;
		sleep 130;

		}
		if (bMoving)
		{
		//step 1
		turn rthigh to x-axis <41> speed <307.500000>;
		turn rleg to x-axis <-30> speed <225.000000>;
		turn rfoot to x-axis <-12> speed <90.000000>;
		turn lthigh to x-axis <-60> speed <337.500000>;
		turn lleg to x-axis <25> speed <75.000000>;
		turn lfoot to x-axis <15> speed <270.000000>;

		move pelvis to y-axis [-0.5] speed [3.750000];
		turn pelvis to z-axis <2> speed <15.000000>;
		turn pelvis to x-axis <1.5> speed <11.250000>;

		sleep 130;
		}
		if (bMoving)
		{

		//step 2
		turn rthigh to x-axis <55> speed <105.000000>;
		turn rleg to x-axis <-20> speed <75.000000>;
		turn rfoot to x-axis <-15> speed <22.500000>;
		turn lthigh to x-axis <-40> speed <150.000000>;
		turn lleg to x-axis <25> now;
		turn lfoot to x-axis <10> speed <37.500000>;

		turn torso to y-axis <5> speed <37.500000>;

		move pelvis to y-axis [-0.5] now;
		turn pelvis to z-axis <3> speed <7.500000>;
		turn pelvis to x-axis <3> speed <11.250000>;

		sleep 130;
		}
		if (bMoving)
		{

		//step 3
		turn rthigh to x-axis <-15> speed <525.000000>;
		turn rleg to x-axis <35> speed <412.500000>;
		turn rfoot to x-axis <-21> speed <45.000000>;

		turn lthigh to x-axis <0> speed <300.000000>;
		turn lleg to x-axis <0> speed <187.500000>;
		turn lfoot to x-axis <0> speed <75.000000>;

		turn torso to y-axis <0> speed <37.500000>;

		move pelvis to y-axis [0] speed [3.750000];
		turn pelvis to z-axis <0> speed <22.500000>;
		turn pelvis to x-axis <0> speed <22.500000>;

		sleep 130;
		}
		if (bMoving)
		{

		//step 4
		turn rthigh to x-axis <-60> speed <337.500000>;
		turn rleg to x-axis <25> speed <75.000000>;
		turn rfoot to x-axis <15> speed <270.000000>;
		turn lthigh to x-axis <41> speed <307.500000>;
		turn lleg to x-axis <-30> speed <225.000000>;
		turn lfoot to x-axis <-12> speed <90.000000>;

		move pelvis to y-axis [-0.5] speed [3.750000];
		turn pelvis to z-axis <-2> speed <15.000000>;
		turn pelvis to x-axis <1.5> speed <11.250000>;

		sleep 130;
		}
		
		//step 5
		turn rthigh to x-axis <-40> speed <150.000000>;
		turn rleg to x-axis <25> now;
		turn rfoot to x-axis <10> speed <37.500000>;

		turn lthigh to x-axis <55> speed <105.000000>;
		turn lleg to x-axis <-20> speed <75.000000>;
		turn lfoot to x-axis <-15> speed <22.500000>;

		move pelvis to y-axis [-0.5] now;
		turn pelvis to z-axis <-3> speed <7.500000>;
		turn pelvis to x-axis <3> speed <11.250000>;

		sleep 130;
		
	
	}
}


HitByWeapon(anglex, anglez)
{
	turn pelvis to z-axis <0> - anglez speed <105.000000>;
	turn pelvis to x-axis <0> - anglex speed <105.000000>;
	wait-for-turn pelvis around z-axis;
	wait-for-turn pelvis around x-axis;
	turn pelvis to z-axis <0.000000> speed <30.000000>;
	turn pelvis to x-axis <0.000000> speed <30.000000>;
}

Go()
{
	Static_Var_2 = 1;
	if( !Static_Var_3 )
	{
		turn lturret to z-axis <0.000000> now;
		turn lturret to z-axis <-90> speed <360>;
		turn rturret to z-axis <0.000000> now;
		turn rturret to z-axis <90> speed <360>;
		sleep 250;
		Static_Var_3 = 1;
	}
}

Stop()
{
	if( Static_Var_3 )
	{
		Static_Var_3 = 0;
		turn rturret to z-axis <0> speed <360>;
		turn lturret to z-axis <0> speed <360>;
		sleep 250;
	}
	Static_Var_2 = 0;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
		else
		{
			call-script Go();
			currentstate = 0;
		}
	}
	statechg_StateChanging = FALSE;
}

MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		aiming = Static_Var_2;
		if( moving )
		{
			call-script walk();
			justmoved = TRUE;
		}
		else
		{
			if( justmoved )
			{
				turn rthigh to x-axis <0> speed <600.000000>;
				turn rleg to x-axis <0> speed <375.000000>;
				turn rfoot to x-axis <0> speed <150.000000>;
				turn lthigh to x-axis <0> speed <600.000000>;
				turn lleg to x-axis <0> speed <375.000000>;
				turn lfoot to x-axis <0> speed <150.000000>;
		
		
				move pelvis to y-axis [0] speed [7.500000];
				turn pelvis to z-axis <0> speed <45.000000>;
				turn pelvis to x-axis <0> speed <45.000000>;
				if( !aiming )
				{
					turn torso to y-axis <0.000000> speed <90.000000>;
				}
				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

UnitSpeed()
{
	while(TRUE)
	{
		currentSpeed = (get CURRENT_SPEED)*160/moveSpeed;
		if (currentSpeed<20) currentSpeed=20;
		sleep 142;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script UnitSpeed();
	start-script MotionControl();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from pelvis;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	hide rflare;
	hide lflare;
	bMoving = FALSE;
	Static_Var_2 = 0;
	Static_Var_3 = 0;
	gun_3 = torso;
	restore_delay = 3000;
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
	moveSpeed = get MAX_SPEED;
	currentSpeed = 100;
	start-script SmokeUnit();
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 5;
}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

SweetSpot(piecenum)
{
	piecenum = torso;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	set-signal-mask 0;
	turn torso to y-axis <0.000000> speed <90.000000>;
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
}

QueryPrimary(piecenum)
{
	piecenum = rflare + gun_3;;
}

AimTertiary(heading, pitch)
{
	start-script RequestState(0);
	signal SIG_AIM_3;
	set-signal-mask SIG_AIM_3;
	while( !Static_Var_3 )
	{
		sleep 100;
	}
	turn torso to y-axis heading speed <275.000000>;
	wait-for-turn torso around y-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireTertiary()
{
	if( gun_3 )
	{
		move lbarrel to z-axis [-2] speed [35.000000];
		wait-for-move lbarrel along z-axis;
		emit-sfx 1024 from rexhaust;
		move lbarrel to z-axis [0.000000] speed [4.000000];
	}
	else
	{
		move rbarrel to z-axis [-2] speed [35.000000];
		wait-for-move rbarrel along z-axis;
		emit-sfx 1024 from lexhaust;
		move rbarrel to z-axis [0.000000] speed [4.000000];
	}
	gun_3 = !gun_3;
}

AimFromTertiary(piecenum)
{
	piecenum = torso;
}

QueryTertiary(piecenum)
{
	piecenum = rflare + gun_3;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode pelvis type BITMAPONLY;
		explode rthigh type BITMAPONLY;
		explode rleg type BITMAPONLY;
		explode rfoot type FIRE | SMOKE | FALL;
		explode lthigh type BITMAPONLY;
		explode lleg type BITMAPONLY;
		explode lfoot type BITMAPONLY;
		explode torso type BITMAPONLY;
		explode lturret type BITMAPONLY;
		explode lbarrel type BITMAPONLY;
		explode lflare type BITMAPONLY;
		explode lexhaust type BITMAPONLY;
		explode rturret type BITMAPONLY;
		explode rbarrel type BITMAPONLY;
		explode rflare type BITMAPONLY;
		explode rexhaust type BITMAPONLY;
		return(0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode pelvis type FIRE | SMOKE | FALL;
		explode rthigh type FALL;
		explode rleg type FIRE | SMOKE | FALL;
		explode rfoot type FALL;
		explode lthigh type FIRE | SMOKE | FALL;
		explode lleg type FIRE | SMOKE | FALL;
		explode lfoot type FIRE | SMOKE | FALL;
		explode torso type FIRE | SMOKE | FALL;
		explode lturret type FALL;
		explode lbarrel type FALL;
		explode lflare type FALL;
		explode lexhaust type FIRE | SMOKE | FALL;
		explode rturret type FALL;
		explode rbarrel type FIRE | SMOKE | FALL;
		explode rflare type FIRE | SMOKE | FALL;
		explode rexhaust type FIRE | SMOKE | FALL;
		return(0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode pelvis type FIRE | SMOKE | FALL;
		explode rthigh type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode rleg type FIRE | SMOKE | FALL;
		explode rfoot type EXPLODE_ON_HIT | SMOKE | FALL;
		explode lthigh type EXPLODE_ON_HIT | SMOKE | FALL;
		explode lleg type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode lfoot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode torso type EXPLODE_ON_HIT | SMOKE | FALL;
		explode lturret type FIRE | SMOKE | FALL;
		explode lbarrel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode lflare type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode lexhaust type FIRE | SMOKE | FALL;
		explode rturret type FIRE | SMOKE | FALL;
		explode rbarrel type SMOKE | FALL;
		explode rflare type SMOKE | FALL;
		explode rexhaust type FIRE | SMOKE | FALL;
		return(0);
	}
	corpsetype = 3 ;
		explode pelvis type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode rthigh type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode rleg type EXPLODE_ON_HIT | FIRE | FALL;
		explode rfoot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode lthigh type EXPLODE_ON_HIT | FIRE | FALL;
		explode lleg type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode lfoot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode torso type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode lturret type EXPLODE_ON_HIT | FIRE | FALL;
		explode lbarrel type EXPLODE_ON_HIT | FIRE | FALL;
		explode lflare type EXPLODE_ON_HIT | FIRE | FALL;
		explode lexhaust type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode rturret type EXPLODE_ON_HIT | FIRE | FALL;
		explode rbarrel type EXPLODE_ON_HIT | FIRE | FALL;
		explode rflare type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode rexhaust type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
}
