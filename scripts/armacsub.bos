#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, rfin, lfin, door, prop, turret, gun, nano1, nano2;

static-var  notBuilding, statechg_DesiredState, statechg_StateChanging;

// Signal definitions
#define SIG_MOVE		2
#define SIG_BUILD		4	

activatescr()
{
	move turret to y-axis [11] speed [22];
	sleep 500;
}

deactivatescr()
{
	set INBUILDSTANCE to 0;
	turn gun to y-axis <0.000000> speed <160.038462>;
	wait-for-turn gun around y-axis;

	move turret to y-axis [0] speed [22];
	sleep 500;
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	spin prop around z-axis speed <360>;
	move door to y-axis [2.5] speed [10];
	move lfin to x-axis [-14] speed [14];
	move rfin to x-axis [14] speed [14];
	
	
	while( TRUE )
	{
		emit-sfx 259 from prop;
		sleep 250;
	}
}

StopMoving()
{	
	signal SIG_MOVE;
	stop-spin prop around z-axis;
	move door to y-axis [0] speed [10];
	move lfin to x-axis [0] speed [14];
	move rfin to x-axis [0] speed [14];

}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState )
		{
			call-script deactivatescr();
			notBuilding = 1;
			currentstate = 1;
		}
		else
		{
			call-script activatescr();
			notBuilding = 0;
			currentstate = 0;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	notBuilding = 1;
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

Activate()
{
	start-script RequestState(0);
}

Deactivate()
{
	start-script RequestState(1);
}

StartBuilding(heading, pitch)
{
	start-script RequestState(0);
	while( notBuilding ) sleep 100;
	signal SIG_BUILD;
	turn gun to y-axis heading speed <160.038462>;
	wait-for-turn gun around y-axis;
	set INBUILDSTANCE to 1;
}

StopBuilding()
{
	signal SIG_BUILD;	
	set-signal-mask SIG_BUILD;
	sleep 6000;
	set-signal-mask 0;
	signal SIG_BUILD;	
	start-script RequestState(1);
}

QueryNanoPiece(piecenum)
{
	piecenum = nano1;
}

/*
TargetHeading(Func_Var_1)
{
	Static_Var_1 = 0 - Func_Var_1;
}
*/

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type BITMAPONLY | BITMAP1;

		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type BITMAPONLY | BITMAP1;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode base type BITMAPONLY | BITMAP1;
		return (0);
	}
	corpsetype = 3;
	explode base type BITMAPONLY | BITMAP1;
}
