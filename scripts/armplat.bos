#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, plat1,plat2,tower,nc1,nc2,towercover,nano2,beam1,beam2,beam3,beam4,nano1,nano3,nano4,nc3,nc4,nanotower3,nanotower4,pad;
static-var  spray, unitviewer, statechg_DesiredState, statechg_StateChanging;

// Signal definitions
#define SIG_ACTIVATE			2


activatescr()
{
	move base to y-axis [37] speed [17.5];

	turn towercover to x-axis <-90> speed <90>;
	turn plat1 to x-axis <90> speed <45>;
	turn plat2 to x-axis <-90> speed <45>;
	
	
	move nanotower3 to y-axis [4.5] speed [4.5];
	move nanotower4 to y-axis [4.5] speed [4.5];
	sleep 1000;
	
	move tower to y-axis [26] speed [26];
	
	move nc1 to z-axis [-7.5] speed [7.5];
	move nc2 to z-axis [7.5] speed [7.5];
	
	turn nc3 to z-axis <-90> speed <90>;
	turn nc4 to z-axis <-90> speed <90>;
	sleep 1000;
	set ARMORED to 0;	
}

deactivatescr()
{
	move base to y-axis [0] speed [17.5];

	move tower to y-axis [0] speed [26];
	turn plat1 to x-axis <0> speed <45>;
	turn plat2 to x-axis <0> speed <45>;
	
	
	turn nc3 to z-axis <0> speed <90>;
	turn nc4 to z-axis <0> speed <90>;
	sleep 1000;
	
	turn towercover to x-axis <0> speed <90>;
	
	move nc1 to z-axis [0] speed [7.5];
	move nc2 to z-axis [0] speed [7.5];
	
	move nanotower3 to y-axis [0] speed [4.5];
	move nanotower4 to y-axis [0] speed [4.5];
	sleep 1000;
	set ARMORED to 1;
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

OpenYard()
{
	set YARD_OPEN to 1;
	while( !get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 1;
	}
	set BUGGER_OFF to 0;
}

CloseYard()
{
	set YARD_OPEN to 0;
	while( get YARD_OPEN )
	{
		set BUGGER_OFF to 1;
		sleep 1500;
		set YARD_OPEN to 0;
	}
	set BUGGER_OFF to 0;
}

Go()
{

	call-script activatescr();
	call-script OpenYard();
	set INBUILDSTANCE to 1;
}

Stop()
{
	set INBUILDSTANCE to 0;
	call-script CloseYard();
	call-script deactivatescr();

}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState )
		{
			call-script Stop();
			currentstate = 1;
		}
		else
		{
			call-script Go();
			currentstate = 0;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{

	set ARMORED to 1;
	unitviewer = FALSE;
	spray = 0;
	move base to y-axis [2.500000] now;
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 1000;
	}
	move base to y-axis [0.000000] speed [5.000000];
	start-script SmokeUnit();
}

QueryNanoPiece(piecenum)
{
	piecenum=beam1+spray;
	spray=spray+1;
	if (spray>3) spray=0;
}

Activate()
{
	signal SIG_ACTIVATE;
	start-script RequestState(0);
}

Deactivate()
{
	signal SIG_ACTIVATE;
	set-signal-mask SIG_ACTIVATE;

	set-signal-mask 0;
	start-script RequestState(1);
}

StartBuilding()
{
	spin pad around y-axis speed <30.005495>;
}

StopBuilding()
{
	stop-spin pad around y-axis;
}

QueryBuildInfo(piecenum)
{
	piecenum = pad;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type BITMAPONLY | BITMAP1;
		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type BITMAPONLY | BITMAP1;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode base type BITMAPONLY | BITMAP1;
		return (0);
	}
	corpsetype = 3;
	explode base type BITMAPONLY | BITMAP1;
}
