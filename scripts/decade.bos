#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, turret1, turret2, abarrela, abarrelb, bbarrela, bbarrelb, flare11,
       flare12, flare21, flare22, ground;

static-var  gun_1, gun_2, restore_delay, oldHead;

// Signal definitions
#define SIG_AIM				2
#define SIG_AIM_2			4
#define SIG_MOVE			8


SetDirection(heading)
{
	var spd;
	spd = (get ABS(oldHead) - get ABS(heading)) / 2;
	turn ground to y-axis heading speed spd;
	turn base to y-axis 0-heading speed spd;
	oldHead = heading;
}

RockOnWaves()
{
	var rockDir, rockAng, rockSpeed;
	rockDir = <-0.1>;
	rockAng = <-0.1>;
	while( TRUE )
	{
		while( get ABS(rockAng) < <1.31> )
		{
			rockSpeed = <1.45> - get ABS(rockAng);
			turn ground to x-axis rockAng speed rockSpeed*5;
			wait-for-turn ground around x-axis;
			rockAng = rockAng + rockDir;
		}
		rockDir = 0 - rockDir;
		rockAng = rockAng + rockDir + rockDir;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script RockOnWaves();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	hide flare11;
	hide flare12;
	hide flare21;
	hide flare22;
	gun_1 = base;
	gun_2 = base;
	restore_delay = 3000;
	start-script SmokeUnit();
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 2;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn turret1 to y-axis <0.> speed <90.>;
	turn turret2 to y-axis <0.> speed <90.>;
	turn abarrela to x-axis <0.> speed <50.>;
	turn abarrelb to x-axis <0.> speed <50.>;
	turn bbarrela to x-axis <0.> speed <50.>;
	turn bbarrelb to x-axis <0.> speed <50.>;
}

waveSplash()
{
	while( TRUE )
	{
		turn base to x-axis <-3.0> speed <3.0>;
		move base to y-axis [0.23] speed [0.23];
		wait-for-turn base around x-axis;
		turn base to x-axis <0.0> speed <3.0>;
		move base to y-axis [0.0] speed [0.23];
		wait-for-turn base around x-axis;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	var waketime;
	start-script waveSplash();
	while( TRUE )
	{
		emit-sfx 2 from ground;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if( waketime < 50 ) waketime = 50;
		sleep 9000 / waketime;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	turn base to x-axis <0.0> speed <3.0>;
	move base to y-axis [0.0] speed [0.23];
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret1 to y-axis heading speed <270.>;
	turn abarrela to x-axis <0.> - pitch speed <150.>;
	turn abarrelb to x-axis <0.> - pitch speed <150.>;
	wait-for-turn turret1 around y-axis;
	wait-for-turn abarrela around x-axis;
	wait-for-turn abarrelb around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	if( gun_1 )
	{
		move abarrelb to z-axis [-1.] speed [500.];
		wait-for-move abarrelb along z-axis;
		move abarrelb to z-axis [0.] speed [5.];
		show flare12;
		sleep 150;
		hide flare12;
	}
	else
	{
		move abarrela to z-axis [-1.] speed [500.];
		wait-for-move abarrela along z-axis;
		move abarrela to z-axis [0.] speed [5.];
		show flare11;
		sleep 150;
		hide flare11;
	}
	gun_1 = !gun_1;
}

AimFromPrimary(piecenum)
{
	piecenum = turret1;
}

AimSecondary(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	turn turret2 to y-axis heading speed <270.>;
	turn bbarrela to x-axis <0.> - pitch speed <150.>;
	turn bbarrelb to x-axis <0.> - pitch speed <150.>;
	wait-for-turn turret2 around y-axis;
	wait-for-turn bbarrela around x-axis;
	wait-for-turn bbarrelb around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireSecondary()
{
	if( gun_2 )
	{
		move bbarrelb to z-axis [-1.] speed [500.];
		wait-for-move bbarrelb along z-axis;
		move bbarrelb to z-axis [0.] speed [5.];
		show flare22;
		sleep 150;
		hide flare22;
	}
	else
	{
		move bbarrela to z-axis [-1.] speed [500.];
		wait-for-move bbarrela along z-axis;
		move bbarrela to z-axis [0.] speed [5.];
		show flare21;
		sleep 150;
		hide flare21;
	}
	gun_2 = !gun_2;
}

AimFromSecondary(piecenum)
{
	piecenum = turret2;
}

QueryPrimary(piecenum)
{
	piecenum = flare11 + gun_1;
}

QuerySecondary(piecenum)
{
	piecenum = flare21 + gun_2;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode ground type BITMAPONLY;
		explode base type BITMAPONLY;
		explode turret1 type BITMAPONLY;
		explode abarrelb type FIRE | SMOKE | FALL;
		explode flare12 type BITMAPONLY;
		explode abarrela type BITMAPONLY;
		explode flare11 type BITMAPONLY;
		explode turret2 type BITMAPONLY;
		explode bbarrelb type BITMAPONLY;
		explode flare22 type BITMAPONLY;
		explode bbarrela type BITMAPONLY;
		explode flare21 type BITMAPONLY;
		return(0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode ground type FIRE | SMOKE | FALL;
		explode base type FIRE | SMOKE | FALL;
		explode turret1 type FIRE | SMOKE | FALL;
		explode abarrelb type FIRE | SMOKE | FALL;
		explode flare12 type FIRE | SMOKE | FALL;
		explode abarrela type FIRE | SMOKE | FALL;
		explode flare11 type FIRE | SMOKE | FALL;
		explode turret2 type FIRE | SMOKE | FALL;
		explode bbarrelb type FALL;
		explode flare22 type FIRE | SMOKE | FALL;
		explode bbarrela type FIRE | SMOKE | FALL;
		explode flare21 type FALL;
		return(0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode ground type FIRE | SMOKE | FALL;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode turret1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode abarrelb type EXPLODE_ON_HIT | SMOKE | FALL;
		explode flare12 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode abarrela type FIRE | SMOKE | FALL;
		explode flare11 type SMOKE | FALL;
		explode turret2 type SMOKE | FALL;
		explode bbarrelb type FIRE | SMOKE | FALL;
		explode flare22 type EXPLODE_ON_HIT | SMOKE | FALL;
		explode bbarrela type EXPLODE_ON_HIT | SMOKE | FALL;
		explode flare21 type SMOKE | FALL;
		return(0);
	}
	corpsetype = 3 ;
		explode ground type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode base type EXPLODE_ON_HIT | FIRE | FALL;
		explode turret1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode abarrelb type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode flare12 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode abarrela type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode flare11 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode turret2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode bbarrelb type EXPLODE_ON_HIT | FIRE | FALL;
		explode flare22 type EXPLODE_ON_HIT | FIRE | FALL;
		explode bbarrela type EXPLODE_ON_HIT | FIRE | SMOKE | FALL;
		explode flare21 type EXPLODE_ON_HIT | FIRE | FALL;
}
